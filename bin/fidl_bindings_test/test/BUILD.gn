# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/environments.gni")
import("//tools/fidl/gidl-conformance-suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")
import("//topaz/runtime/dart/dart_fuchsia_test.gni")
import("//topaz/runtime/dart_runner/dart_app.gni")

generated_source = "$target_gen_dir/conformance_test.dart"

gidl("conformance_suite") {
  testonly = true
  language = "dart"
  inputs = conformance_suite_gidl_files
  fidl = conformance_suite_fidl_target
  output = generated_source
}

dart_fuchsia_test("fidl_bindings_test") {
  sources = [
    "channel_close_test.dart",
    "clone_test.dart",
    "codec_test.dart",
    "error_test.dart",
    "event_test.dart",
    "failure_test.dart",
    "hash_test.dart",
    "header_test.dart",
    "oneway_test.dart",
    "state_test.dart",
    "tostring_test.dart",
    "twoway_test.dart",
    rebase_path(generated_source, get_label_info("fidl_bindings_test", "dir")),
  ]

  meta = [
    {
      path = rebase_path("meta/fidl_bindings_test.cmx")
      dest = "fidl_bindings_test.cmx"
    },
  ]

  deps = [
    "//third_party/dart-pkg/pub/collection",
    "//topaz/bin/fidl_bindings_test/fidl:bindings_test",
    "//topaz/lib/gidl",
    "//topaz/public/dart/fidl",
    "//topaz/public/dart/fuchsia_services",
    conformance_suite_fidl_target,
  ]
  non_dart_deps = [ ":conformance_suite" ]
  environments = basic_envs
}
